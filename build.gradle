
plugins {
    id 'java'
    id 'application'
    id 'antlr'
    id 'jacoco'
}

mainClassName = 'basic.driver.Main'

dependencies {
    compile 'org.apache.bcel:bcel:6.2'
    antlr 'org.antlr:antlr4:4.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

repositories {
    mavenCentral()
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-encoding", "utf-8", "-no-listener", "-visitor"]
}

compileJava {
    options.encoding = "UTF-8"
}

run {
    classpath += files(project.projectDir.getPath() + '/build/libs/basic-rt.jar')
	args project.projectDir.getPath() + '/cases/case11.bas'
}

javadoc {
    options.addBooleanOption('html5', true)
    options.encoding "UTF-8"
}

task runtimeJar(type: Jar) {
    archiveBaseName = 'basic-rt'
    from sourceSets.main.output
    from sourceSets.main.java
    include 'basic/runtime/**'
}

artifacts {
    archives runtimeJar
}

test {
    classpath += files(project.projectDir.getPath() + '/build/libs/basic-rt.jar')
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.3"
    reportsDir = file("${buildDir}/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
